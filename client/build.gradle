plugins {
    id 'billing-profile-manager.library-conventions'
    id "org.hidetake.swagger.generator" version "2.19.1"
}

dependencies {
    ext {
        jerseyVersion = "2.35"
        jacksonDatatypeVersion = "2.13.1"
    }

    // Swagger deps, versions controlled by dependencyManagement in root project
    implementation group: "com.fasterxml.jackson.datatype", name: "jackson-datatype-jsr310", version: jacksonDatatypeVersion
    implementation group: "org.glassfish.jersey.core", name: "jersey-client", version: jerseyVersion
    implementation group: "org.glassfish.jersey.media", name: "jersey-media-json-jackson", version: jerseyVersion
    implementation group: "org.glassfish.jersey.media", name: "jersey-media-multipart", version: jerseyVersion
    implementation group: "javax.annotation", name: "javax.annotation-api", version: "1.3.2"
    implementation gradle.librarySwaggerAnnotations
    swaggerCodegen gradle.librarySwaggerCli
}

// OpenAPI/Swagger Client Generation
def artifactGroup = "${group}.profile"
def openapiOutputDir = "${buildDir}/openapi"

swaggerSources {
    client {
        inputFile = file(gradle.openapiSourceFile)
        code {
            language = "java"
            // For Swagger Codegen v3 on Java 16+
            // See https://github.com/swagger-api/swagger-codegen/issues/10966
            jvmArgs = ['--add-opens=java.base/java.util=ALL-UNNAMED']
            library = "jersey2"
            templateDir = file("${projectDir}/src/main/resources/swaggercodegen")
            outputDir = file(openapiOutputDir)
            rawOptions = [
                "--model-package", "${artifactGroup}.model",
                "--api-package", "${artifactGroup}.api",
                "--invoker-package", "${artifactGroup}.client",
                "--group-id", "${artifactGroup}",
                "--artifact-version", "${version}",
                "--ignore-file-override", "${projectDir}/.swagger-codegen-ignore",
                "-D", "apiTests=false," +
                      "apiDocs=false," +
                      "modelTests=false," +
                      "modelDocs=false," +
                      "dateLibrary=java8"
            ]
        }
    }
}

// Internal dependencies
sourceSets.main.java.srcDir "${openapiOutputDir}/src/main/java"
compileJava.dependsOn swaggerSources.client.code
