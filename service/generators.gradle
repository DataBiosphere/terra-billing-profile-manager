dependencies {
    implementation 'io.swagger.core.v3:swagger-annotations'
    runtimeOnly 'org.webjars.npm:swagger-ui-dist:4.5.0'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli'

    // Versioned by Spring:
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'org.webjars:webjars-locator-core'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

def artifactGroup = "${group}.profile"

generateSwaggerCode {
    inputFile = file('src/main/resources/api/service_openapi.yaml')
    language = 'spring'
    components = ['models', 'apis']
    additionalProperties = [
        modelPackage     : "${artifactGroup}.model",
        apiPackage       : "${artifactGroup}.api",
        dateLibrary      : 'java11',
        java8            : true,
        interfaceOnly    : 'true',
        useTags          : 'true',
        jakarta          : 'true',
        springBootVersion: dependencyManagement.managedVersions['org.springframework.boot:spring-boot']
    ]
}

String swaggerOutputSrc = "${generateSwaggerCode.outputDir}/src/main/java"

idea.module.generatedSourceDirs = [file(swaggerOutputSrc)]
sourceSets.main.java.srcDir swaggerOutputSrc
compileJava.dependsOn generateSwaggerCode

// see https://github.com/n0mer/gradle-git-properties
gitProperties {
    keys = []
    customProperty('bpm.version.gitTag', { it.describe(tags: true) })
    customProperty('bpm.version.gitHash', { it.head().abbreviatedId })
    customProperty('bpm.version.github', { "https://github.com/DataBiosphere/terra-billing-profile-manager/tree/${it.describe(tags: true)}" })
    customProperty('bpm.version.build', version)
}
