plugins {
	id 'bio.terra.profile.java-spring-conventions'
	id 'de.undercouch.download'
	id 'com.google.cloud.tools.jib'

	id 'com.gorylenko.gradle-git-properties' version '2.4.1'
	id 'org.sonarqube' version '4.4.1.3373'
	id "au.com.dius.pact" version "4.3.19"

}

apply from: 'generators.gradle'
apply from: 'publishing.gradle'

dependencies {
	implementation 'io.sentry:sentry:6.32.0'

	implementation('org.yaml:snakeyaml') {
		version {
			require "1.32"
		}
		because 'previous versions have a security vulnerability - this is a direct dependency to force the version to override opencensus'
	}
	implementation 'bio.terra:terra-common-lib:0.0.94-SNAPSHOT'

	implementation 'org.apache.commons:commons-dbcp2'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.retry:spring-retry'
	implementation group: "org.apache.commons", name: "commons-lang3"

	implementation 'org.springframework.boot:spring-boot-starter-actuator:2.7.17'
	implementation 'io.micrometer:micrometer-registry-prometheus'

	// Google dependencies
	implementation platform('com.google.cloud:libraries-bom:26.25.0')

	// Cloud Resource Library
	implementation group: "com.azure.resourcemanager", name: "azure-resourcemanager-managedapplications", version: "1.0.0-beta.3"
	implementation group: "com.azure.resourcemanager", name: "azure-resourcemanager-subscription", version: "1.0.0-beta.2"
	implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-costmanagement', version: "1.0.0-beta.6"
	implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-containerservice', version: '2.19.0'

	implementation group: 'bio.terra', name: 'terra-cloud-resource-lib', version: "1.2.7-SNAPSHOT"

	// Sam
	implementation group: "org.broadinstitute.dsde.workbench", name: "sam-client_2.13", version: "0.1-fd8ee25"

	// Transitive dependency constraints due to security vulnerabilities in prior versions.
	// These are not directly included, they are just constrained if they are pulled in as
	// transitive dependencies.
	constraints {
		implementation('org.json:json:20231013')
	}

	// Test deps
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	// Allows us to mock final classes
	testImplementation 'org.mockito:mockito-inline:5.2.0'

	testImplementation 'au.com.dius.pact.consumer:junit5:4.6.3'
	testImplementation("au.com.dius.pact.provider:junit5:4.6.3")
	testImplementation("au.com.dius.pact.provider:junit5spring:4.6.3")
}
test {
	systemProperties['pact.rootDir'] = "$buildDir/pacts"
	systemProperties['pact.provider.version'] = "$project.version"
}
sonarqube {
	properties {
		property "sonar.projectName", "terra-billing-profile-manager"
		property "sonar.projectKey", "DataBiosphere_terra-billing-profile-manager"
		property "sonar.organization", "broad-databiosphere"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.sources", "src/main/java"
	}
}

tasks.register("pactTests", Test) {
	useJUnitPlatform {
		includeTags "pact-test"
	}
	testLogging {
		showStandardStreams = true
	}
}

tasks.register("verifyPacts", Test) {
	useJUnitPlatform {
		includeTags "provider-test"
	}
	testLogging {
		showStandardStreams = true
	}
	outputs.upToDateWhen { false } // always run this task even if it's up to date

	systemProperty 'pact.provider.version', System.getenv('PACT_PROVIDER_COMMIT')
	systemProperty 'pact.provider.branch', System.getenv('PACT_PROVIDER_BRANCH')
	systemProperty 'pact.verifier.publishResults', true
	systemProperty 'pactbroker.host', System.getenv('PACT_BROKER_URL')
	systemProperty 'pactbroker.auth.username', System.getenv('PACT_BROKER_USERNAME')
	systemProperty 'pactbroker.auth.password', System.getenv('PACT_BROKER_PASSWORD')
	systemProperty 'pactbroker.scheme', 'https'
}
